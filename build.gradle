plugins {
    id 'maven-publish'
    alias catalog.plugins.lombok apply false
    alias catalog.plugins.multi.release.jar
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: catalog.plugins.lombok.get().pluginId

    group = "net.woggioni"
    version = getProperty('wson.version')

    lombok {
        version = catalog.versions.lombok.get()
    }

    repositories {
        maven {
            url = "https://woggioni.net/mvn/"
            content {
                includeGroup 'com.beust'
                includeGroup 'com.lys'
                includeGroup 'net.woggioni'
            }
        }
        mavenCentral()
    }

    dependencies {
        implementation catalog.xz

        testImplementation catalog.junit.jupiter.api
        testImplementation catalog.junit.jupiter.params
        testRuntimeOnly catalog.junit.jupiter.engine
    }

    test {
        useJUnitPlatform()
    }

    java {
        modularity.inferModulePath = true
    }
}

ext {
    setProperty('jpms.module.name', 'net.woggioni.wson')
}

dependencies {
    implementation catalog.jwo
    implementation catalog.slf4j.api

    testImplementation project('test-utils')
    testImplementation catalog.jackson.databind
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.compilerArgs << '-parameters'
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = getProperty("gradle.version")
}


publishing {
    repositories {
        maven {
            url = "https://mvn.woggioni.net/"
        }
    }
    publications {
        maven(MavenPublication) {
            from(components["java"])
        }
    }
}
