plugins {
    id 'maven-publish'
    id 'net.woggioni.gradle.lombok' apply false
    id 'net.woggioni.gradle.multi-release-jar'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'net.woggioni.gradle.lombok'

    group = "net.woggioni"
    version = getProperty('wson.version')

    lombok {
        version = getProperty('lombok.version')
    }

    repositories {
        maven {
            url = "https://woggioni.net/mvn/"
            content {
                includeGroup 'net.woggioni'
                includeGroup 'com.lys'
            }
        }
        mavenCentral()
    }

    dependencies {
        implementation platform(group: 'com.lys', name: 'lys-dependencies', version: getProperty('lys.version'))
        implementation group: "org.tukaani", name: "xz"

        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }
}

dependencies {
    implementation group: "net.woggioni", name: "jwo"
    implementation group: "org.slf4j", name: "slf4j-api"

    testImplementation project('test-utils')
    testImplementation group: "com.fasterxml.jackson.core", name:  "jackson-databind"
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.release = 8
    options.compilerArgs << '-parameters'
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = getProperty("gradle.version")
}


publishing {
    repositories {
        maven {
            url = "https://mvn.woggioni.net/"
        }
    }
    publications {
        maven(MavenPublication) {
            from(components["java"])
        }
    }
}
